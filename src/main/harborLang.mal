/*
 * Copyright 2024-2025 Stockholm University <https://www.su.se>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
#id: "org.mal-lang.harborLang"
#version: "1.0.0"

// Abstract Base Classes
category AbstractBase {

  abstract asset CyberSystem {
    | compromised
  }

  abstract asset CommunicationSystem extends CyberSystem {
    | dataManipulation
  }

  abstract asset NavigationSystem extends CyberSystem {
    | spoofedData
    | failure  
  }

  abstract asset MaritimeAsset {
    | operationalDisruption
    | incorrectFleetRouting  
  }
}

// Cyber Layer (Navigation, Communication, and Traffic Systems)
category CyberLayer {

  asset GPS extends NavigationSystem {
    | gpsJamming -> ins.coordinatesDrifting
    | gpsSpoofing -> ins.adjustmentToFalseCoordinates
    # signalEncryption
  }

  asset AIS extends CommunicationSystem {
    | aisSpoofing -> falseAISDataToTrafficManagement, falseAISDataToVessel
    | aisDataManipulation -> falseAISDataToVessel
    | falseAISDataToTrafficManagement -> trafficManagement.falseVesselTracking, trafficManagement.incorrectFleetRouting
    | falseAISDataToVessel -> vessel.receivedFalseAIS, vessel.collisionRisk
    | falseAISData -> vessel.falsePosition
  }

  asset TrafficManagement extends CommunicationSystem {
    | falseVesselTracking -> incorrectFleetRouting
    | incorrectFleetRouting -> vessel.routeDeviation
    | operationalDisruption -> portInfrastructure.operationalDisruption
  }
}

// Cyber-Physical Layer
category CyberPhysicalLayer {

  asset INS extends NavigationSystem {
    | coordinatesDrifting -> vessel.routeLoss  
    | adjustmentToFalseCoordinates -> vessel.routeDeviation
  }

  asset PortInfrastructure extends MaritimeAsset {
    | operationalDisruption
  }
}

// Physical Layer
category PhysicalLayer {

  asset Vessel extends MaritimeAsset {
    | routeLoss
    | routeDeviation -> collisionRisk
    | collisionRisk -> manualCorrection
    | falsePosition -> collisionRisk, operationalDisruption 
    | receivedFalseAIS -> falsePosition, collisionRisk 
    | manualCorrection -> routeDeviation  
    | unexpectedWaitingTime
  }

  asset HumanOperator extends MaritimeAsset {
    | detectsCyberThreat -> vessel.manualCorrection
    | overridesNavigation -> vessel.manualCorrection  
    | incorrectManualCorrection -> vessel.routeDeviation
    | misinterpretsAIS -> vessel.routeDeviation
  }
}

// System Failures and Human Factors
category SystemFailures {

  asset SystemFailure {
    | hardwareMalfunction -> gps.gpsJamming
    | softwareBug -> ais.aisDataManipulation
  }

  asset HumanError {
    | misinterpretation -> vessel.routeDeviation
    | delayedResponse -> vessel.collisionRisk
    | incorrectManualCorrection -> vessel.manualCorrection
  }
}

// Associations (Asset Relationships)
associations {
  GPS [gps] 1 <-- CommunicatesWith --> 1 [ins] INS
  AIS [ais] 1 <-- SendsDataTo --> 1 [vessel] Vessel  
  AIS [ais] 1 <-- CommunicatesWith --> 1 [trafficManagement] TrafficManagement  
  TrafficManagement [trafficManagement] 1 <-- Manages --> 1 [vessel] Vessel  
  TrafficManagement [trafficManagement] 1 <-- ManagedBy --> 1 [portInfrastructure] PortInfrastructure  
  INS [ins] 1 <-- Guides --> 1 [vessel] Vessel  
  HumanOperator [humanOperator] 1 <-- Oversees --> 1 [vessel] Vessel  
  SystemFailure [systemFailure] 1 <-- Disrupts --> 1 [gps] GPS  
  SystemFailure [systemFailure] 1 <-- Affects --> 1 [ais] AIS  
  HumanError [humanError] 1 <-- Causes --> 1 [vessel] Vessel  
}